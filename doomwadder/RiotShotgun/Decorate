ACTOR RiotShotgun : Shotgun 10092
{
  +WEAPON.NOALERT
  Weapon.SelectionOrder 1200
  Weapon.Slotnumber 3
  Weapon.AmmoType1 "Shell"
  Weapon.AmmoType2 "RicoShell"
  Decal "BulletChip"
  Weapon.UpSound "weapons/riotraise"
  Inventory.PickupMessage "Picked up a Riot Shotgun."
  Obituary "%o was blasted by %k's Riot Shotgun."
  Tag "Riot Shotgun"
  States
  {
  DryFire:
	RSHT A 5
	
  Ready:
    RSHT A 1 A_WeaponReady
    Loop
	
	//Remove the comments before the (15) to make raising and/or lowering faster.
	
  Deselect:
    RSHT A 1 A_Lower//(15)
    Loop
	
  Select:
    RSHT A 1 A_Raise//(15)
    Loop

  AltFire:
	RSHT A 0
	{
		if(CountInv("PelletsSelected")==1)
			{
				A_LOG("\crBUCKSHOT ROUNDS \cLSELECTED",2.0);
				A_TakeInventory("PelletsSelected",1);
			}
		else
			{
				A_LOG("\ciRICOCHET ROUNDS \cLSELECTED",2.0);
				A_GiveInventory("PelletsSelected",1);
			}
	}
	RSHT BC 1 A_WeaponOffset(-16,-2,WOF_ADD|WOF_INTERPOLATE)
	RSHT CD 1 A_WeaponOffset(-12,-1,WOF_ADD|WOF_INTERPOLATE)
	RSHT E 1 A_WeaponOffset(-8,-3,WOF_ADD|WOF_INTERPOLATE)
	RSHT E 1
	{
		A_WeaponOffset(-3,1,WOF_ADD|WOF_INTERPOLATE);
		A_StartSound("weapons/riotpumpdown",CHAN_AUTO);
	}
	RSHT E 1 A_WeaponOffset(-6,3,WOF_ADD|WOF_INTERPOLATE)
	RSHT K 0 A_JumpIfInventory("PelletsSelected",1,3)
	RSHT N 1 A_WeaponOffset(-12,6,WOF_ADD|WOF_INTERPOLATE)
	RSHT N 0 A_Jump(256,2)
	RSHT K 1 A_WeaponOffset(-12,6,WOF_ADD|WOF_INTERPOLATE)
	RSHT LH 1 A_WeaponOffset(-9,2,WOF_ADD|WOF_INTERPOLATE)
	RSHT I 1 A_WeaponOffset(-9,8,WOF_ADD|WOF_INTERPOLATE)
	RSHT II 1 A_WeaponOffset(-3,1,WOF_ADD|WOF_INTERPOLATE)
	RSHT I 1
	RSHT II 1 A_WeaponOffset(3,-1,WOF_ADD|WOF_INTERPOLATE)
	RSHT I 1 A_WeaponOffset(9,-8,WOF_ADD|WOF_INTERPOLATE)
	RSHT J 0 A_JumpIfInventory("PelletsSelected",1,"AltLoading")
	RSHT J 1
	{
		A_WeaponOffset(12,-6,WOF_ADD|WOF_INTERPOLATE);
		A_StartSound("weapons/riotpumpup",CHAN_AUTO);
	}
	RSHT K 1 A_WeaponOffset(9,-2,WOF_ADD|WOF_INTERPOLATE)
	Goto PumpUp
	
  AltLoading:
	RSHT M 1
	{
		A_WeaponOffset(12,-6,WOF_ADD|WOF_INTERPOLATE);
		A_StartSound("weapons/riotpumpup",CHAN_AUTO);
	}
	RSHT N 1 A_WeaponOffset(9,-2,WOF_ADD|WOF_INTERPOLATE)
	
  PumpUp:
	RSHT L 1 A_WeaponOffset(9,-2,WOF_ADD|WOF_INTERPOLATE)
	RSHT E 1 A_WeaponOffset(8,3,WOF_ADD|WOF_INTERPOLATE)
	RSHT DC 1 A_WeaponOffset(12,1,WOF_ADD|WOF_INTERPOLATE)
	RSHT CB 1 A_WeaponOffset(16,2,WOF_ADD|WOF_INTERPOLATE)
	RSHT A 1 A_WeaponOffset(0,38)
	RSHT AA 1 A_WeaponOffset(0,-3,WOF_ADD|WOF_INTERPOLATE)
	RSHT A 1 A_WeaponOffset(0,32)
	Goto Ready
  
	FirePellets:
	RSHF A 0
	{
		if(countinv("RicoShell")>=1)
			{
				A_FireProjectile("RubberPellets",frandom(-5.5,5.5),false,0,6,0,frandom(-1.5,3.5));
				A_StartSound("weapons/riotgunalt",CHAN_BODY);
				if (!CountInv("PowerInfiniteAmmo") && !GetCVar("sv_infiniteammo"))
					{
					A_TakeInventory("RicoShell",1);
					}
			}
		else
			{
				A_StartSound("weapons/riotclick");
				return state("DryFire"); //Change "Dryfire" to "Altfire" if you want this to automatically change back to regular shells when empty.
			}
		return state("");
		
	}
	RSHF AAAAAAAA 0 A_FireProjectile("RubberPellets",frandom(-5.5,5.5),false,0,6,0,frandom(-1.5,3.5))
	RSFH A 0 A_Jump(256,4)
	
	//Fallthrough; it skips the original firing function and continues with the frames after.
	
  Fire:
	RSHT A 0 A_AlertMonsters
	RSHT A 0 A_JumpIfInventory("PelletsSelected",1,"FirePellets")
	RSFH A 0
	{
		A_StartSound("weapons/riotshotgunfire",CHAN_BODY);
		A_FireBullets(8,8,12,10,"BulletPuff",FBF_NORANDOM|FBF_USEAMMO);
	}
    RSHF A 1
	{
		A_AttachLightDef("ShotgunFlashSmall","ShotgunFlash1");
		A_WeaponOffset(0,6,WOF_ADD|WOF_INTERPOLATE);
		//A_QuakeEx(2,2,2,8,0,32,"",QF_3D); Un-comment this if you want an extra screen shake to the firing.
		A_SetPitch(pitch-1.0,SPF_INTERPOLATE);
	}
	RSHF B 1
	{
		A_RemoveLight("ShotgunFlashSmall");
		A_AttachLightDef("ShotgunFlashLarge","ShotgunFlash2");
		A_WeaponOffset(0,3,WOF_ADD|WOF_INTERPOLATE);
		A_SetPitch(pitch-0.5,SPF_INTERPOLATE);
	}
	RSHF A 1
	{
		A_RemoveLight("ShotgunFlashLarge");
		A_AttachLightDef("ShotgunFlashSmall","ShotgunFlash1");
		A_WeaponOffset(0,3,WOF_ADD|WOF_INTERPOLATE);
		A_SetPitch(pitch-0.5,SPF_INTERPOLATE);
	}
	RSHT A 1
	{
		A_RemoveLight("ShotgunFlashSmall");
		A_WeaponOffset(0,-6,WOF_ADD|WOF_INTERPOLATE);
		A_SetPitch(pitch+1.0,SPF_INTERPOLATE);
	}
	RSHT AA 1
	{
		A_WeaponOffset(0,-6,WOF_ADD|WOF_INTERPOLATE);
		A_SetPitch(pitch+0.5,SPF_INTERPOLATE);
	}
    RSHT BC 1 A_WeaponOffset(-16,-2,WOF_ADD|WOF_INTERPOLATE)
	RSHT CD 1 A_WeaponOffset(-12,-1,WOF_ADD|WOF_INTERPOLATE)
	RSHT E 1 A_WeaponOffset(-8,-3,WOF_ADD|WOF_INTERPOLATE)
	RSHT E 1
	{
		A_WeaponOffset(-3,1,WOF_ADD|WOF_INTERPOLATE);
		A_StartSound("weapons/riotpumpdown",CHAN_AUTO);
	}
	RSHT E 1 A_WeaponOffset(-6,3,WOF_ADD|WOF_INTERPOLATE)
	RSHT F 1 A_WeaponOffset(-12,6,WOF_ADD|WOF_INTERPOLATE)
	RSHT GH 1 A_WeaponOffset(-9,2,WOF_ADD|WOF_INTERPOLATE)
	RSHT I 1 A_WeaponOffset(-9,8,WOF_ADD|WOF_INTERPOLATE)
	RSHT II 1 A_WeaponOffset(-3,1,WOF_ADD|WOF_INTERPOLATE)
	RSHT I 1
	RSHT II 1 A_WeaponOffset(3,-1,WOF_ADD|WOF_INTERPOLATE)
	RSHT I 1 A_WeaponOffset(9,-8,WOF_ADD|WOF_INTERPOLATE)
	RSHT I 0 A_JumpIfInventory("PelletsSelected",1,"AltReloading")
	
  NormalReload:
	RSHT J 1
	{
		A_WeaponOffset(12,-6,WOF_ADD|WOF_INTERPOLATE);
		A_StartSound("weapons/riotpumpup",CHAN_AUTO);
	}
	RSHT K 1 A_WeaponOffset(9,-2,WOF_ADD|WOF_INTERPOLATE)
	Goto ReloadingFinish
	
  AltReloading:
    RSHT M 1
	{
		A_WeaponOffset(12,-6,WOF_ADD|WOF_INTERPOLATE);
		A_StartSound("weapons/riotpumpup",CHAN_AUTO);
	}
	RSHT N 1 A_WeaponOffset(9,-2,WOF_ADD|WOF_INTERPOLATE)
	
  ReloadingFinish:
	RSHT L 1 A_WeaponOffset(9,-2,WOF_ADD|WOF_INTERPOLATE)
	RSHT E 1 A_WeaponOffset(8,3,WOF_ADD|WOF_INTERPOLATE)
	RSHT DC 1 A_WeaponOffset(12,1,WOF_ADD|WOF_INTERPOLATE)
	RSHT CB 1 A_WeaponOffset(16,2,WOF_ADD|WOF_INTERPOLATE)
	RSHT A 1 A_WeaponOffset(0,38)
	RSHT AA 1 A_WeaponOffset(0,-3,WOF_ADD|WOF_INTERPOLATE)
	RSHT A 1 A_WeaponOffset(0,32)
    RSHT A 3 A_ReFire
    Goto Ready
	
  Spawn:
    RTGR A -1
    Stop
  }
}

ACTOR RubberPellets
{
  +BLOODSPLATTER
  BounceType Doom
  BounceCount 2
  BounceFactor 0.7
  WallBounceFactor 0.7
  Radius 2
  Height 4
  Speed 45
  Damage (11)
  Decal "BulletChip"
  Projectile
  RenderStyle Add
  Alpha 0.75
  Scale 0.8
  BounceSound "weapons/riotricochet"
  DeathSound "weapons/riotricochet"
  Obituary "%o was hit by %k's ricochet."
  States
  {
  Spawn:
    RPEL A 1 Bright
    Loop
  Death:
    PUFF ABCD 2 Bright
    Stop
  }
}

ACTOR PelletsSelected : Inventory
{
     Inventory.MaxAmount 1
}

ACTOR RicoShell : Ammo 10093
{
  Inventory.PickupMessage "Picked up 4 ricoshells."
  Inventory.Amount 4
  Inventory.MaxAmount 40
  Ammo.BackpackAmount 4
  Ammo.BackpackMaxAmount 80
  Inventory.Icon "RSHLA0"
  States
  {
  Spawn:
    RSHL A -1
    Stop
  }
}

ACTOR RicoShellBox : RicoShell 10094
{
  Inventory.PickupMessage "Picked up a box of ricoshells."
  Inventory.Amount 20
  States
  {
  Spawn:
    RBOX A -1
    Stop
  }
}